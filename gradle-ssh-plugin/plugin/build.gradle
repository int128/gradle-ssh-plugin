plugins {
	id 'groovy'
	id 'groovy-gradle-plugin'
	id 'maven-publish'
	id 'com.gradle.plugin-publish' version '1.3.1'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation gradleApi()
	implementation project(':groovy-ssh:core')
	testImplementation 'junit:junit:4.13.2'
	testImplementation 'org.spockframework:spock-core:2.4-groovy-5.0'
	testImplementation 'cglib:cglib-nodep:3.3.0'
}

test {
	useJUnitPlatform()
}

group = 'org.hidetake'
version = System.getenv('VERSION') ?: 'SNAPSHOT'

gradlePlugin {
	website = 'https://github.com/int128/gradle-ssh-plugin'
	vcsUrl = 'https://github.com/int128/gradle-ssh-plugin'

	plugins {
		ssh {
			id = 'org.hidetake.ssh'
			displayName = 'Gradle SSH Plugin'
			description = 'A plugin for remote command execution and file transfer via SSH'
			tags.set(['remote', 'ssh', 'deploy'])
			implementationClass = 'org.hidetake.gradle.ssh.plugin.SshPlugin'
		}
	}
}
