plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile('org.hidetake:groovy-ssh:2.10.1') {
        exclude module: 'groovy-all'
    }
    testCompile('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.2.10'
}

group = 'org.hidetake'
version = System.getenv('CIRCLE_TAG') ?: 'SNAPSHOT'

processResources {
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['version': project.version])
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.group
            artifactId parent.name
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: findProperty('bintrayUser')
    key  = System.getenv('BINTRAY_KEY')  ?: findProperty('bintrayKey')
    publications = ['maven']
    publish = true
    pkg {
        repo = 'maven'
        name = parent.name
        version {
            attributes = ['gradle-plugin': 'org.hidetake.ssh:org.hidetake:gradle-ssh-plugin']
            name = project.version
        }
    }
}
