plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    api localGroovy()
    api 'com.github.mwiede:jsch:0.2.5'
    api 'org.slf4j:slf4j-api:2.0.10'

    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.spockframework:spock-junit4"
    testRuntimeOnly 'cglib:cglib-nodep:3.3.0'
    testRuntimeOnly 'org.objenesis:objenesis:3.3'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.4.14'
}

test {
    useJUnitPlatform()
}

processResources {
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['version': project.version])
}

java {
    withJavadocJar()
    withSourcesJar()
}

description = 'Groovy SSH library'

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            // https://central.sonatype.org/publish/requirements/
            artifactId parent.name
            from components.java
            pom {
                name = parent.name
                description = project.description
                url = 'https://github.com/int128/groovy-ssh'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'int128'
                        name = 'Hidetake Iwata'
                        email = 'int128@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/int128/groovy-ssh'
                    connection = 'scm:git:https://github.com/int128/groovy-ssh'
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(System.getenv("SIGNING_KEY"), System.getenv("SIGNING_PASSWORD"))
    sign publishing.publications
}
