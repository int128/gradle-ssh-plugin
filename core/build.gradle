plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.6'
}

group = 'org.hidetake'
version = System.getenv('CIRCLE_TAG') ?: 'SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile('org.hidetake:groovy-ssh:2.4.0') {
        exclude module: 'groovy-all'
    }
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.1'
}

processResources {
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['version': project.version])
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.group
            artifactId rootProject.name
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: findProperty('bintrayUser')
    key  = System.getenv('BINTRAY_KEY')  ?: findProperty('bintrayKey')
    publications = ['maven']
    publish = true
    pkg {
        repo = 'maven'
        name = rootProject.name
        version {
            attributes = ['gradle-plugin': 'org.hidetake.ssh:org.hidetake:gradle-ssh-plugin']
            name = rootProject.version
        }
    }
}
