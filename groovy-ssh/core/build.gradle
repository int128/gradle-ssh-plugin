import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'groovy'
    id "com.vanniktech.maven.publish" version "0.33.0"
}

repositories {
    mavenCentral()
}

dependencies {
    api localGroovy()
    api 'com.github.mwiede:jsch:2.27.4'
    api 'org.slf4j:slf4j-api:2.0.17'

    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.spockframework:spock-junit4"
    testRuntimeOnly 'cglib:cglib-nodep:3.3.0'
    testRuntimeOnly 'org.objenesis:objenesis:3.5'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.5.27'
}

description = 'Groovy SSH library'
group = 'org.hidetake'
version = System.getenv('VERSION') ?: 'SNAPSHOT'

test {
    useJUnitPlatform()
}

processResources {
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['version': project.version])
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
    pom {
        name = parent.name
        description = project.description
        url = 'https://github.com/int128/gradle-ssh-plugin'
        licenses {
            license {
                name = 'Apache-2.0'
                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'int128'
                name = 'Hidetake Iwata'
                url = 'https://github.com/int128'
            }
        }
        scm {
            url = 'https://github.com/int128/gradle-ssh-plugin'
            connection = 'scm:git:https://github.com/int128/gradle-ssh-plugin'
        }
    }
}
