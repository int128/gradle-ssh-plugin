description 'Automated acceptance test with SSH server'

buildscript {
    def version = file('../gradle.properties').withReader {
        def properties = new Properties()
        properties.load(it)
        properties.version
    }
    repositories {
        ivy { url = '../build/ivy' }
        mavenCentral()
    }
    dependencies {
        classpath "org.hidetake:gradle-ssh-plugin:$version"
    }
}

class FeatureDelegate {
    String task = null
    boolean aggressiveTest = false
    String aggressiveReason = null

    void task(String path) {
        assert task == null, 'feature task is already set'
        task = path
    }
    void aggressiveTest(String reason) {
        aggressiveTest = true
        aggressiveReason = reason
    }
}

def feature(String name, Closure featureClosure) {
    def feature = new FeatureDelegate()
    featureClosure.delegate = feature
    featureClosure.resolveStrategy = Closure.DELEGATE_FIRST
    featureClosure.call()

    if (feature.task) {
        if (feature.aggressiveTest) {
            tasks.aggressiveTest.dependsOn(feature.task)
            logger.info("Added feature: $name (aggressive test because ${feature.aggressiveReason})")
        } else {
            tasks.test.dependsOn(feature.task)
            logger.info("Added feature: $name")
        }
    } else {
        logger.warn "Feature ignored: $name"
    }
}

task test {
    description = 'Run tests'
}

task aggressiveTest {
    description = 'Run tests which may cause modification of the environment'
}

apply from: 'features.gradle'
