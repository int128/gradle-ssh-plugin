apply plugin: 'ssh'

ssh {
    config(StrictHostKeyChecking: 'no')
}

remotes {
    localhost {
        role 'testServers'
        host = 'localhost'
        user = System.properties['user.name']
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
    sudoWithPassword {
        host = 'localhost'
        user = "user${randomInt()}p"
        password = "pass${randomInt()}"
    }
    sudoNoPassword {
        host = 'localhost'
        user = "user${randomInt()}n"
        password = "pass${randomInt()}"
    }
}


feature('executing a command') {
    task 'executeCommand'
}

task executeCommand(type: SshTask) {
    doFirst {
        ext.x = randomInt()
        ext.y = randomInt()
    }
    session(remotes.localhost) {
        ext.a = execute "expr $x + $y"
    }
    doLast {
        assert a as int == (x + y)
    }
}


feature('filter hosts by role') {
    task 'executeCommandByRole'
}

task executeCommandByRole(type: SshTask) {
    doFirst {
        ext.x = randomInt()
        ext.y = randomInt()
    }
    session(remotes.role('testServers')) {
        ext.a = execute "expr $x + $y"
    }
    doLast {
        assert a as int == (x + y)
    }
}


feature('executing commands sequentially') {
    task 'executeSequentially'
}

task executeSequentially(type: SshTask) {
    doFirst {
        ext.x = randomInt()
        ext.y = randomInt()
        ext.pathA = "/tmp/data-${randomInt()}"
        ext.pathB = "/tmp/data-${randomInt()}"
    }
    session(remotes.localhost) {
        execute "expr $x + $y > $pathA"
        execute "expr $x + `cat $pathA` > $pathB"
        ext.a = execute "cat $pathA && rm $pathA"
        ext.b = execute "cat $pathB && rm $pathB"
    }
    doLast {
        assert a as int == (x + y)
        assert b as int == (x + x + y)
    }
}


feature('each command should have independent environment') {
    task 'testEnvironmentOfCommand'
}

task testEnvironmentOfCommand(type: SshTask) {
    session(remotes.localhost) {
        execute "export testdata=dummy"
        ext.a = execute 'echo "testdata is $testdata"'
    }
    doLast {
        assert a == 'testdata is '
    }
}


feature('executing commands concurrently') {
    task 'executeConcurrently'
}

task executeConcurrently(type: SshTask) {
    doFirst {
        ext.pathA = "/tmp/data-${randomInt()}"
        ext.pathB = "/tmp/data-${randomInt()}"
        ext.pathC = "/tmp/data-${randomInt()}"
        ext.pathD = "/tmp/data-${randomInt()}"
    }
    // task should start sessions concurrently
    session(remotes.localhost) {
        executeBackground "sleep 3 && date +%s > $pathA"
    }
    session(remotes.localhost) {
        executeBackground "sleep 2 && date +%s > $pathB"
        executeBackground "sleep 1 && date +%s > $pathC"
        executeBackground            "date +%s > $pathD"
    }
    doLast {
        // all commands should be completed at this point
        sshexec {
            session(remotes.localhost) {
                ext.timeA = execute("cat $pathA && rm $pathA")
                ext.timeB = execute("cat $pathB && rm $pathB")
                ext.timeC = execute("cat $pathC && rm $pathC")
                ext.timeD = execute("cat $pathD && rm $pathD")
            }
        }
        assert Math.abs((timeA as int) - (timeD as int)) == 3
        assert Math.abs((timeB as int) - (timeD as int)) == 2
        assert Math.abs((timeC as int) - (timeD as int)) == 1
    }
}


feature('handling failure of the remote command') {
    task 'failureStatus'
}

task failureStatus << {
    try {
        sshexec {
            session(remotes.localhost) {
                execute 'exit 1'
            }
        }
        assert false, 'an exception should be thrown'
    } catch(RuntimeException e) {
        assert e.localizedMessage.contains('status 1')
    }
}


feature('handling failure of the remote command in background') {
    task 'failureStatusInBackground'
}

task failureStatusInBackground << {
    try {
        sshexec {
            session(remotes.localhost) {
                executeBackground 'exit 1'
            }
        }
        assert false, 'an exception should be thrown'
    } catch(RuntimeException e) {
        assert e.localizedMessage.contains('status 1')
    }
}


feature('executing a privileged command by sudo') {
    task 'executeSudoWithPassword'
    aggressiveTest 'requires creating an user and setting up sudoers'
}

task executeSudoWithPassword(type: SshTask) {
    dependsOn   'createUserForSudoWithPassword'
    finalizedBy 'deleteUserForSudoWithPassword'

    session(remotes.sudoWithPassword) {
        ext.whoami = executeSudo('whoami', pty: true)
    }
    doLast {
        assert whoami == 'root'
    }
}

task createUserForSudoWithPassword(type: SshTask) {
    finalizedBy 'deleteUserForSudoWithPassword'

    session(remotes.localhost) {
        def username = remotes.sudoWithPassword.user
        def password = remotes.sudoWithPassword.password
        execute("sudo useradd -m $username")
        execute("sudo passwd $username") {
            interaction {
                when(partial: ~/.+[Pp]assword: */) {
                    standardInput << password << '\n'
                }
            }
        }
        execute("echo '$username ALL=(ALL) ALL' > /tmp/$username")
        execute("sudo chmod 440 /tmp/$username")
        execute("sudo chown 0.0 /tmp/$username")
        execute("sudo mv /tmp/$username /etc/sudoers.d")
    }
}

task deleteUserForSudoWithPassword(type: SshTask) {
    session(remotes.localhost) {
        def username = remotes.sudoWithPassword.user
        execute("sudo rm -v /etc/sudoers.d/$username")
        execute("sudo userdel -r $username")
    }
}


feature('executing a privileged command by sudo without password') {
    task 'executeSudoNoPassword'
    aggressiveTest 'requires creating an user and setting up sudoers'
}

task executeSudoNoPassword(type: SshTask) {
    dependsOn 'createUserForSudoNoPassword'
    finalizedBy 'deleteUserForSudoNoPassword'

    session(remotes.localhost) {
        ext.whoami = executeSudo('whoami', pty: true)
    }
    doLast {
        assert whoami == 'root'
    }
}

task createUserForSudoNoPassword(type: SshTask) {
    finalizedBy 'deleteUserForSudoNoPassword'

    session(remotes.localhost) {
        def username = remotes.sudoNoPassword.user
        def password = remotes.sudoNoPassword.password
        execute("sudo useradd -m $username")
        execute("sudo passwd $username") {
            interaction {
                when(partial: ~/.+[Pp]assword: */) {
                    standardInput << password << '\n'
                }
            }
        }
        execute("echo '$username ALL=(ALL) NOPASSWD: ALL' > /tmp/$username")
        execute("sudo chmod 440 /tmp/$username")
        execute("sudo chown 0.0 /tmp/$username")
        execute("sudo mv /tmp/$username /etc/sudoers.d")
    }
}

task deleteUserForSudoNoPassword(type: SshTask) {
    session(remotes.localhost) {
        def username = remotes.sudoNoPassword.user
        execute("sudo rm -v /etc/sudoers.d/$username")
        execute("sudo userdel -r $username")
    }
}


feature('executing a shell') {
    task 'executeShell'
}

task executeShell(type: SshTask) {
    session(remotes.localhost) {
        shell {
            interaction {
                when(partial: ~/.*$/) {
                    standardInput << 'exit 0' << '\n'
                }
            }
        }
    }
}


feature('sending files to the remote host and acquiring result files') {
    task 'putAndGetFiles'
}

task putAndGetFiles(type: SshTask) {
    doFirst {
        ext.x = randomInt()
        ext.y = randomInt()

        buildDir.delete()
        buildDir.mkdir()
        file("$buildDir/X") << x
        file("$buildDir/Y") << y

        ext.pathX = "/tmp/data-${randomInt()}"
        ext.pathY = "/tmp/data-${randomInt()}"
        ext.pathA = "/tmp/data-${randomInt()}"
        ext.pathB = "/tmp/data-${randomInt()}"
    }
    session(remotes.localhost) {
        put("$buildDir/X", pathX)
        put("$buildDir/Y", pathY)
        execute("expr `cat $pathX` + `cat $pathY` > $pathA")
        execute("expr `cat $pathX` - `cat $pathY` > $pathB")
        get(pathA, "$buildDir/A")
        get(pathB, "$buildDir/B")
    }
    doLast {
        assert file("$buildDir/A").text as int == (x + y)
        assert file("$buildDir/B").text as int == (x - y)
    }
}


static randomInt(int max = 10000) {
    (Math.random() * max) as int
}
