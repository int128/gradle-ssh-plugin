plugins {
    id 'groovy'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

configure([compileJava, compileGroovy]) {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
    if (System.getenv('JAVA_HOME_7')) {
        options.bootClasspath = "${System.getenv('JAVA_HOME_7')}/jre/lib/rt.jar"
    }
}

mainClassName = 'org.hidetake.groovy.ssh.Main'

dependencies {
    compile project(':core')
    compile 'ch.qos.logback:logback-classic:1.1.2'

    runtime 'commons-cli:commons-cli:1.2'

    testCompile project(':server-integration-test')

    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testRuntime 'cglib:cglib-nodep:3.2.1'
    testRuntime 'org.objenesis:objenesis:2.2'
}

test {
    mustRunAfter ':server-integration-test:check'

    if (System.getProperty('os.name') == 'Linux') {
        systemProperty 'java.security.egd', 'file:/dev/./urandom'
    }
}

shadowJar {
    baseName = 'gssh'
    classifier = ''
    version = ''
}

runShadow {
    args "$projectDir/gssh-example.groovy"
    workingDir project(':os-integration-test').buildDir
}

check.dependsOn runShadow
