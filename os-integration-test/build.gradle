plugins {
    id 'groovy'
}

dependencies {
    compile project(':core')

    compile 'org.spockframework:spock-core:1.2-groovy-2.5'

    testRuntime 'ch.qos.logback:logback-classic:1.1.2'
}

test {
    mustRunAfter ':server-integration-test:check'
}

task startSshAgent(type: Exec) {
    commandLine 'ssh-agent'
    standardOutput = new ByteArrayOutputStream()
    doLast {
        standardOutput.toString().eachMatch(~/(.+?)=(.+?);/) { all, k, v ->
            assert k in ['SSH_AGENT_PID', 'SSH_AUTH_SOCK']
            [test, stopSshAgent]*.environment(k, v)
        }
    }
}

task stopSshAgent(type: Exec) {
    commandLine 'ssh-agent', '-k'
}

test.dependsOn startSshAgent
test.finalizedBy stopSshAgent
