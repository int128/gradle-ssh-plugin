allprojects {
    group = 'org.hidetake'
    version = System.getenv('CIRCLE_TAG') ?: 'SNAPSHOT'

    repositories {
        jcenter()
    }

    afterEvaluate {
        tasks.withType(Test) {
            maxHeapSize = '1g'
            minHeapSize = '1g'

            finalizedBy ':testReport'
            testReport.reportOn binResultsDir
            reports.html.enabled = false
            reports.junitXml.destination = file("${rootProject.buildDir}/test-results")
        }
    }
}

task resolveDependencies << {
    allprojects.each { project ->
        project.buildscript.configurations*.resolve()
        project.configurations*.resolve()
    }
}

task testReport(type: TestReport) {
    description 'Generates test report for all projects'
    destinationDir = file("$buildDir/reports")
}
