apply plugin: 'groovy'
apply plugin: 'project-report'

description = 'Gradle SSH Plugin provides remote execution and file transfer capabilities'
group = 'org.hidetake'
version = '0.1.5'

repositories {
	mavenCentral()
}

dependencies {
	compile gradleApi()
	groovy localGroovy()

	compile 'com.jcraft:jsch:0.1.48'
	testCompile 'org.spockframework:spock-core:0.7-groovy-1.8', 'cglib:cglib-nodep:2.2.2'
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
	classifier = 'javadoc'
	from "${buildDir}/javadoc"
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

if (gradle.startParameter.taskNames.contains('uploadArchives')) {
	apply from: 'build.publish.gradle'
}
